#!/usr/bin/env zsh
# shellcheck shell=sh disable=3043,2034,3003,2296,2298,1037,3006,2154,3024,1009,1073,1072,1050,1083,1140,1036,1088
#
# Copyright (c) 2019 Sebastian Gniazdowski
# Copyright (c) 2021 Z-Shell ZI Contributors

∧za-default-ice-main-cmd() {
    # Set the base and typically useful options.
    builtin emulate -LR zsh
    builtin setopt extendedglob warncreateglobal typesetsilent noshortloops

    autoload -Uz is-at-least
    local is_58
    is-at-least 5.8 && is_58=1

    integer retval
    typeset -g REPLY
    typeset -ga reply
    typeset -gA Reply

    [[ $1 == default-ice ]] && { shift; integer eas_subcommand=1; }
    integer ibdebug=$+ICE[debug]

    # -D - clear out the recognized options
    # -E - allow mixing of options and non-options
    # -M - allow private arrays for the options
    #
    # The options are:
    # --with/--mod/-m/--use – to pass the desired module(s)
    local -a help clear show stats get quiet
    zparseopts -D -E -M ${is_58:+} -clear=clear c=clear \
        -show=show s=show -stats=stats t=stats -get=get g=get \
        -quiet=quiet q=quiet h=help -help=help || retval=1
        if [[ $retval -gt 0 || $1 = -* ]] {
            +zinit-message "{nl}{error}default-ice annex: {msg}Incorrect options" \
                "given${${(M)1:#-*}:+ "(bad option: {ehi}$1{rst}{msg})"}." \
                "Usage:{rst}{nl}"
            help=( -h ) retval=1
        }

    if (( !$# && !${#help} && !${#show} && !${#clear} && !${#stats} )) {
        +zinit-message "{ehi}default-ice annex:{rst}{msg} Missing any" \
            "ices. Usage:{rst}{nl}"
        help=( -h ) retval=1
    }

    # --help/-h
    if (( ${#help} )) {
        ∧za-default-ice-main-cmd-help-handler
        return retval
    }

    # --show/-s
    if (( ${#show} )) {
        .za-default-ice-show-default-ices
        return $?
    }

    # --stats/-t
    if (( ${#stats} )) {
        .za-default-ice-stats
        return $?
    }

    local -A ZI_ICES
    .zinit-ice "$@"
    integer iceret=$?

    shift iceret
    if (( $# )) {
        +zinit-message "{error}default-ice:{rst}{msg} Unrecognized ice detected{ehi}:" \
            "{nb}{error}$1{msg}.{rst}"
        return 1
    }
    Plugins[DEFAULT_ICE_default-ices]=${(j: :)${(@qkv)ZI_ICES}}
    (( ${#quiet} )) || +zinit-message "{pre}default-ice:{rst}{msg} stored the ices{ehi}:{rst}" \
                                "${(j: :)${(@q-kv)ZI_ICES}}.{rst}"
    return 0
}
