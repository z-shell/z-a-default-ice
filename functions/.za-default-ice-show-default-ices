# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2019 Sebastian Gniazdowski
# Copyright (c) 2021-present Z-Shell Community

.za-default-ice-show-default-ices() {

  # Set the base and typically useful options.
  builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
  builtin setopt extended_glob warn_create_global typeset_silent no_short_loops rc_quotes no_auto_pushd

  if [[ $Plugins[DEFAULT_ICE_default-ices] == [[:space:]]# ]] {
    (( ${#quiet} )) || +zi-message "{annex}default-ice-annex{ehi}:{rst}{info2} No ices currently set{rst}"
    return 1
  }

  local -A ice
  ice=( "${(@Q)${(@z)Plugins[DEFAULT_ICE_default-ices]}}" )

  local k v
  for k v ( "${(kv)ice[@]}" ) {
    +zi-message "{b}{ice}$k{ehi}:{rst}{tab}{data}$v{rst}"
  }
  return 0
}
